plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.112'
    id 'idea'
}

tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    // Add here additional repositories if required by some of the dependencies below.
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven {
        name = "Geckolib"
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
    }
    // Local libs folder for tharidia_tweaks and azurelib
    flatDir {
        dirs 'libs'
    }
}

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users in 1.21.1, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        client2 {
            // This defines a new run named 'client2'
            client() // This tells NeoGradle to set it up as a client run

            // Set the username for this run
            programArguments = ['--username', 'Dev2']

            // This is highly recommended to avoid conflicts
            gameDirectory = project.file('runs/client2') 
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Task to copy latest JARs from sibling projects
tasks.register('copyLatestJars', Copy) {
    description = 'Copy the latest JARs from Tharidia Features and Tweaks projects'
    group = 'build'
    
    // Copy Tharidia Features JAR
    from('../Tharidia_features/build/libs') {
        include '*.jar'
        exclude '*-sources.jar', '*-javadoc.jar'
        rename { 'tharidiafeatures-latest.jar' }
    }
    
    // Copy Tharidia Tweaks JAR
    from('../Tharidia_tweaks/build/libs') {
        include '*.jar'
        exclude '*-sources.jar', '*-javadoc.jar'
        rename { 'tharidiatweaks-latest.jar' }
    }
    
    into 'libs'
}

// Make compileJava depend on copying latest JARs
compileJava.dependsOn copyLatestJars

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    // AzureLib for animated models (NeoForge version) - local dependency
    //implementation files("libs/azurelib-neo-1.21.1-3.0.38.jar")
    
    // Tharidia Tweaks mod - local dependency (will be in same mods folder)
    // compileOnly for compile-time access, localRuntime for dev environment testing
    // Always uses the latest JAR from the sibling project
    compileOnly files('libs/tharidiatweaks-latest.jar')
    runtimeOnly files('libs/tharidiatweaks-latest.jar')
    
    // Tharidia Features mod - local dependency for world border functionality
    // Always uses the latest JAR from the sibling project
    compileOnly files('libs/tharidiafeatures-latest.jar')
    runtimeOnly files('libs/tharidiafeatures-latest.jar')
    
    // MySQL Connector for database communication - included in JAR via JarJar
    implementation 'com.mysql:mysql-connector-j:8.3.0'
    jarJar(group: 'com.mysql', name: 'mysql-connector-j', version: '[8.3.0,9.0.0)')
    
    // HikariCP for connection pooling - included in JAR via JarJar
    implementation 'com.zaxxer:HikariCP:5.1.0'
    jarJar(group: 'com.zaxxer', name: 'HikariCP', version: '[5.1.0,6.0.0)')
    
    // JLayer for MP3 playback - included in JAR via JarJar
    implementation 'javazoom:jlayer:1.0.1'
    jarJar(group: 'javazoom', name: 'jlayer', version: '[1.0.1,2.0.0)')
    
    // VLCJ for video playback - included in JAR via JarJar
    implementation 'uk.co.caprica:vlcj:4.8.2'
    jarJar(group: 'uk.co.caprica', name: 'vlcj', version: '[4.8.2,5.0.0)')
    
    // VLC Native Discovery
    implementation 'uk.co.caprica:vlcj-natives:4.8.0'
    jarJar(group: 'uk.co.caprica', name: 'vlcj-natives', version: '[4.8.0,5.0.0)')
    
    // JNA for native library access (required by VLCJ)
    // Note: JNA is already provided by Minecraft/NeoForge or other mods, so we don't JarJar it
    implementation 'net.java.dev.jna:jna:5.14.0'
    // jarJar(group: 'net.java.dev.jna', name: 'jna', version: '[5.14.0,6.0.0)')
    
    implementation 'net.java.dev.jna:jna-platform:5.14.0'
    // jarJar(group: 'net.java.dev.jna', name: 'jna-platform', version: '[5.14.0,6.0.0)')
    
    // Gson for JSON parsing (YouTube API responses)
    // Note: Gson is already provided by Minecraft, so we don't JarJar it
    implementation 'com.google.code.gson:gson:2.10.1'
    // jarJar(group: 'com.google.code.gson', name: 'gson', version: '[2.10.1,3.0.0)')
    
    
    // SLF4J API is already provided by Minecraft/NeoForge - no need to include it

    // Paladins mod dependency for stone great hammer
    //implementation "maven.modrinth:paladins-and-priests:2.6.3+1.21.1-neoforge"
    
    // Example optional mod dependency with JEI
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    // compileOnly "mezz.jei:jei-${mc_version}-common-api:${jei_version}"
    // compileOnly "mezz.jei:jei-${mc_version}-neoforge-api:${jei_version}"
    // We add the full version to localRuntime, not runtimeOnly, so that we do not publish a dependency on it
    // localRuntime "mezz.jei:jei-${mc_version}-neoforge:${jei_version}"

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation "blank:coolmod-${mc_version}:${coolmod_version}"

    // Example mod dependency using a file as dependency
    // implementation files("libs/coolmod-${mc_version}-${coolmod_version}.jar")

    // Example project dependency using a sister or child project:
    // implementation project(":myproject")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

tasks.named('jar') {
    archiveBaseName.set('TharidiaThings')
    archiveVersion.set('')
}